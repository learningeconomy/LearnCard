apply plugin: 'com.android.application'

android {
    namespace = 'org.scoutpass.app'
    compileSdk = rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "org.scoutpass.app"
        minSdkVersion = rootProject.ext.minSdkVersion
        targetSdkVersion = rootProject.ext.targetSdkVersion
        versionCode project.hasProperty('versionCode') ? Integer.parseInt(project.versionCode) : 1
        versionName project.hasProperty('versionName') ? project.versionName : "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding true
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
        coreLibraryDesugaringEnabled true        
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation 'androidx.browser:browser:1.3.0'
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
}

/**
 * Camera dependency overrides:
 *
 * Some Capacitor plugins (like @capacitor/camera or @capacitor-mlkit/barcode-scanning)
 * still pull in older versions of AndroidX CameraX (e.g. 1.1.x), which include native `.so`
 * libraries built with 4 KB page alignment.
 *
 * Starting with Android 15+, Google Play requires native libraries to support 16 KB pages.
 * Without this override, builds can include `libimage_processing_util_jni.so` compiled with
 * the older settings, leading to Play Store validation issues or potential runtime errors.
 *
 * Forcing newer CameraX versions here ensures everything is built with 16 KB alignment
 * and keeps the app future-proof on newer Android devices.
 */
configurations.all {
    resolutionStrategy {
        force "androidx.camera:camera-core:1.4.2"
        force "androidx.camera:camera-camera2:1.4.2"
        force "androidx.camera:camera-lifecycle:1.4.2"
        force "androidx.camera:camera-view:1.4.2"
        force "androidx.camera:camera-extensions:1.4.2"
    }
}

ext.postBuildExtras = {
    android {
        compileOptions {
            coreLibraryDesugaringEnabled true
        }
    }
    dependencies {
        coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
    }
}


apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
